/*
 * Copyright 2020 Calin-Andrei Burloiu
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

group 'org.calinburloiu.microtuner'
version '0.2.0-SNAPSHOT'

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.16"
    classpath "com.github.jengelman.gradle.plugins:shadow:2.0.1"
  }
}

apply plugin: 'scala'
apply plugin: 'com.github.maiflai.scalatest'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
  compile group: 'com.typesafe.scala-logging', name: scala('scala-logging'), version: '3.9.2'
  compile group: 'com.beachape', name: scala('enumeratum'), version: '1.5.15'
  compile group: 'com.iheart', name: scala('ficus'), version: '1.4.7'
  compile group: 'com.google.guava', name: 'guava', version: '28.2-jre'
  compile group: 'com.typesafe.play', name: scala('play-json'), version: '2.6.5'
  compile group: 'com.fasterxml.jackson.module', name: scala('jackson-module-scala'), version: '2.9.1'

  // Required to make Java MIDI work on Mac OS X
  compile group: 'uk.co.xfactory-librarians', name: 'coremidi4j', version: '1.0'
//    testCompile group: 'junit', name: 'junit', version: '4.12'

  compile group: 'org.scala-lang', name: 'scala-library', version: scalaVersion

  testCompile group: 'org.scalatest', name: scala('scalatest'), version: '3.0.4'
  testCompile "org.pegdown:pegdown:1.4.2"  // Required by scalatest for generating reports
}

jar {
  manifest {
    attributes 'Main-Class': 'org.calinburloiu.music.microtuner.ScalistApp'
  }
}

shadowJar {
  classifier = 'fat'
  version = ''
}

build.dependsOn(shadowJar)

def scala(String artifactName) {
  return "${artifactName}_${scalaMajorVersion}"
}
