/*
 * Copyright 2020 Calin-Andrei Burloiu
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

plugins {
  id "scala"
  id "com.github.maiflai.scalatest" version "0.26"
  id "com.github.johnrengelman.shadow" version "6.0.0"
}

ext {
  scalaVersion = "2.13.5"
  scalaMajorVersion = "2.13"
}

group "org.calinburloiu.microtuner"
version "0.2.0-SNAPSHOT"

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

tasks.withType(ScalaCompile) {
  scalaCompileOptions.additionalParameters = ["-deprecation", "-feature"]
}

dependencies {
  implementation group: "ch.qos.logback", name: "logback-classic", version: "1.2.3"
  implementation group: "com.typesafe.scala-logging", name: scala("scala-logging"), version: "3.9.2"
  implementation group: "com.beachape", name: scala("enumeratum"), version: "1.6.1"
  implementation group: "com.iheart", name: scala("ficus"), version: "1.5.0"
  implementation group: "com.google.guava", name: "guava", version: "29.0-jre"
  implementation group: "com.typesafe.play", name: scala("play-json"), version: "2.9.1"

  // Required to make Java MIDI work on Mac OS
  implementation group: "uk.co.xfactory-librarians", name: "coremidi4j", version: "1.4"

  implementation group: "org.scala-lang", name: "scala-library", version: scalaVersion

  testImplementation group: "org.scalatest", name: scala("scalatest"), version: "3.2.2"
  testImplementation group: "org.scalamock", name: scala("scalamock"), version: "5.0.0"
  // Required by scalatest for some weird reason
  testRuntimeOnly group: "com.vladsch.flexmark", name: "flexmark-all", version: "0.36.8"
}

jar {
  manifest {
    attributes "Main-Class": "org.calinburloiu.music.microtuner.ScalistApp"
  }
}

build.dependsOn(shadowJar)

def scala(String artifactName) {
  return "${artifactName}_${scalaMajorVersion}"
}
